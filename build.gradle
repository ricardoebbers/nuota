import br.ufpe.cin.recl.nuota.buildsrc.Libs
import br.ufpe.cin.recl.nuota.buildsrc.Urls

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Hilt.gradlePlugin
        classpath Libs.gradle
        classpath Libs.googleServices
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()

        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url Urls.composeSnapshotRepo }
            maven { url Urls.mavenCentralSnapshotRepo }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            jvmTarget = "1.8"

            // Use experimental APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-Xallow-jvm-ir-dependencies'
            freeCompilerArgs += [
                    '-P',
                    'plugin:androidx.compose.compiler.plugins.kotlin:intrinsicRemember=true'
            ]
        }
    }
}