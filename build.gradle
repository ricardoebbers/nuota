import br.ufpe.cin.recl.nuota.buildsrc.Libs
import br.ufpe.cin.recl.nuota.buildsrc.Urls
import br.ufpe.cin.recl.nuota.buildsrc.Versions

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Hilt.gradlePlugin
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()

        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url Urls.composeSnapshotRepo }
            maven { url Urls.mavenCentralSnapshotRepo }
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            ktlint(Versions.ktLint)
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            jvmTarget = "1.8"

            // Use experimental APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-Xallow-jvm-ir-dependencies'
            freeCompilerArgs += [
                    '-P',
                    'plugin:androidx.compose.compiler.plugins.kotlin:intrinsicRemember=true'
            ]
        }
    }
}